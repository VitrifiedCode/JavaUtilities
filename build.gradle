buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "me.champeau.gradle:jmh-gradle-plugin:0.3.1"
    }
}

apply plugin: 'java'
apply plugin: "me.champeau.gradle.jmh"
apply plugin: 'maven-publish'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
//    compile "org.jetbrains:annotations:15.0"
    compile "com.google.code.findbugs:jsr305:1.3.9"
    compile "com.google.guava:guava:19.0"
    compile 'com.google.code.gson:gson:2.8.0'
    compile 'org.fusesource.jansi:jansi:1.12'
    compile 'org.jasypt:jasypt:1.9.2'

    compile "org.apache.commons:commons-collections4:4.1"
    compile "org.apache.commons:commons-lang3:3.5"
    compile "org.apache.commons:commons-compress:1.13"
    compile "commons-io:commons-io:2.5"
    compile "commons-net:commons-net:3.6"
    compile "org.apache.commons:commons-exec:1.3"
    compile "commons-codec:commons-codec:1.10"
    compile "org.apache.commons:commons-math3:3.6.1"
    compile "org.apache.httpcomponents:httpclient:4.5.3"
    compile "commons-validator:commons-validator:1.6"

    testCompile group: 'junit', name: 'junit', version: '4.11'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

jmh {
    if (project.hasProperty("EXECUTE_JMH")) {
        if (project.hasProperty("highAccuracyTest")) {
            iterations = 50
            benchmarkMode = ['avgt']
            jvmArgs = "-Xmx2048M -Xms2000M -XX:+UnlockDiagnosticVMOptions -XX:+UnlockExperimentalVMOptions -XX:hashCode=5 -XX:+UseG1GC -XX:+UseFastAccessorMethods -XX:+UseFastEmptyMethods -XX:+OptimizeStringConcat -XX:+UseStringDeduplication -XX:+AggressiveOpts -XX:ParallelGCThreads=2"
            fork = 4
            forceGC = true
            warmupIterations = 10
            timeUnit = 'ns'
        } else if (project.hasProperty("extremeHighAccuracyTest")) {
            iterations = 100
            benchmarkMode = ['avgt']
            jvmArgs = "-Xmx2048M -Xms2000M -XX:+UnlockDiagnosticVMOptions -XX:+UnlockExperimentalVMOptions -XX:hashCode=5 -XX:+UseG1GC -XX:+UseFastAccessorMethods -XX:+UseFastEmptyMethods -XX:+OptimizeStringConcat -XX:+UseStringDeduplication -XX:+AggressiveOpts -XX:ParallelGCThreads=2"
            fork = 8
            forceGC = true
            warmupIterations = 20
            timeOnIteration = '10s'
            warmup = '10s'
            timeUnit = 'ns'
        } else {
            iterations = 20
            benchmarkMode = ['avgt']
            jvmArgs = "-Xmx2048M -Xms2000M -XX:+UnlockDiagnosticVMOptions -XX:+UnlockExperimentalVMOptions -XX:hashCode=5 -XX:+UseG1GC -XX:+UseFastAccessorMethods -XX:+UseFastEmptyMethods -XX:+OptimizeStringConcat -XX:+UseStringDeduplication -XX:+AggressiveOpts -XX:ParallelGCThreads=2"
            fork = 2
            forceGC = false
            warmupIterations = 10
            timeUnit = 'ns'
        }
    } else {
        iterations = 1
        benchmarkMode = ['avgt']
        fork = 0
        forceGC = false
        warmupIterations = 1
        timeUnit = 'ns'
    }
}

task executeClass(type: JavaExec) {
    main = project.hasProperty('mainClass') ? project.findProperty('mainClass') : ' '
    classpath = sourceSets.main.runtimeClasspath
}

task executeClassTest(type: JavaExec) {
    main = project.hasProperty('mainClass') ? project.findProperty('mainClass') : ' '
    classpath = sourceSets.test.runtimeClasspath
}

ext.incrementVersionNumber = { ->
    def versionPropsFile = file('version.properties')
    def version = "1.0.0"


    if (versionPropsFile.canRead()) {
        def Properties versionProps = new Properties()

        versionProps.load(new FileInputStream(versionPropsFile))

        def versionBuild = versionProps['VERSION_BUILD'].toInteger() + 1

        version = versionProps['VERSION_MAJOR'] + "." + versionProps['VERSION_MINOR'] + "." + versionProps['VERSION_BUILD']

        println version

        versionProps['VERSION_BUILD'] = versionBuild.toString()
        versionProps.store(versionPropsFile.newWriter(), null)
    } else {
        version = '1.0.0'
    }

    return version
}

group 'com.github.vitrifiedcode.javautilities'
version incrementVersionNumber()
